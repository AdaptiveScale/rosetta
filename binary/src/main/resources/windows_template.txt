@if "%DEBUG%" == "" @echo off
@rem ##########################################################################
@rem  Rosetta DBT Studio startup script for Windows
@rem ##########################################################################

@rem Set local scope for the variables
if "%OS%"=="Windows_NT" setlocal

set DIRNAME=%~dp0
if "%DIRNAME%" == "" set DIRNAME=.

set APP_BASE_NAME=rosetta
set APP_HOME=%DIRNAME%..

@rem Add default JVM options here. Can also set JAVA_OPTS externally
set DEFAULT_JVM_OPTS=

@rem Set JAVA_HOME relative to APP_HOME
set JAVA_HOME=%APP_HOME%
set JAVA_EXE=%JAVA_HOME%\bin\java.exe
set JAVA_EXE="%JAVA_EXE:"=%"

if exist %JAVA_EXE% goto init

echo.
echo ERROR: The directory "%JAVA_HOME%" does not contain a valid Java runtime for your platform.
echo Please set the JAVA_HOME variable in your environment to match the
echo location of your Java installation.
goto fail

:init
@rem Capture command-line arguments
set CMD_LINE_ARGS=%*

@rem Set CLASSPATH
set CLASSPATH=%APP_HOME%\lib\*

@rem Set driver path
set "DRIVERS_PATH=%ROSETTA_DRIVERS%"
if "%ROSETTA_DRIVERS%"=="" (
    set "DRIVERS_PATH=%APP_HOME%\drivers"
)

if not exist "%DRIVERS_PATH%" (
    mkdir "%DRIVERS_PATH%"
)

@rem Enable delayed expansion for dynamic variable usage
setlocal EnableDelayedExpansion

@rem Add all driver jars to classpath
for %%f in ("%DRIVERS_PATH%\*.jar") do (
    set CLASSPATH=!CLASSPATH!;%%f
)

@rem Run Rosetta main class
%JAVA_EXE% %DEFAULT_JVM_OPTS% %JAVA_OPTS% -Djdk.tls.client.protocols=TLSv1.2 -Dapp.name=%APP_BASE_NAME% -cp "!CLASSPATH!" com.adaptivescale.rosetta.cli.Main %CMD_LINE_ARGS%

:end
if "%ERRORLEVEL%"=="0" goto mainEnd

:fail
exit /b 1

:mainEnd
if "%OS%"=="Windows_NT" endlocal

:omega
