import org.beryx.runtime.util.Util

plugins {
    id 'java'
    id 'org.beryx.runtime' version '1.12.6'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':cli')
}

application {
    mainClass = 'com.adaptivescale.rosetta.cli.Main'
    applicationName = 'rosetta'
}

ext{
    imageName='rosetta-'+project.version+'.zip'
    imageFile = Util.createRegularFileProperty(project)
    imageFile.set(project.layout.buildDirectory.file(imageName))
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.base', 'java.naming', 'java.xml', 'java.sql', 'java.rmi', 'java.management', 'java.prefs', 'java.desktop', 'jdk.unsupported']

    targetPlatform("linux-x64") {
        imageZip = project.ext.imageFile
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_linux_hotspot_11.0.9.1_1.tar.gz") {
            downloadDir = "$buildDir/jdklinux"
            archiveName = "linux-x64"
            archiveExtension = "tar.gz"
            pathToHome = "jdk-11.0.9.1+1/"
            overwrite = true
        }
    }

    targetPlatform("mac_x64") {
        imageZip = project.ext.imageFile
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_mac_hotspot_11.0.9.1_1.tar.gz") {
            downloadDir = "$buildDir/jdkmac"
            archiveName = "mac"
            archiveExtension = "tar.gz"
            pathToHome = "jdk-11.0.9.1+1/Contents/Home"
            overwrite = true
        }
    }

    targetPlatform("mac_aarch64") {
        imageZip = project.ext.imageFile
        jdkHome = jdkDownload("https://cdn.azul.com/zulu/bin/zulu11.56.19-ca-jdk11.0.15-macosx_aarch64.tar.gz") {
            downloadDir = "$buildDir/jdkmac_arm"
            archiveName = "mac_arm"
            archiveExtension = "tar.gz"
            pathToHome = "zulu11.56.19-ca-jdk11.0.15-macosx_aarch64/zulu-11.jdk/Contents/Home"
            overwrite = true
        }
    }

    targetPlatform("win_x64") {
        imageZip = project.ext.imageFile
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_windows_hotspot_11.0.9.1_1.zip") {
            downloadDir = "$buildDir/jdkwin"
            archiveName = "win"
            archiveExtension = "zip"
            pathToHome = "jdk-11.0.9.1+1"
            overwrite = true
        }
    }

    launcher {
        def resourcesDir = sourceSets.main.resources.srcDirs.first().absolutePath
        unixScriptTemplate = file("file:/${resourcesDir}/unix_template.txt")
        windowsScriptTemplate = file("file:/${resourcesDir}/windows_template.txt")
    }
}
